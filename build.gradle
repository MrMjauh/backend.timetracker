plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'java'
	id 'jacoco'
	id 'checkstyle'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.codecamos'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

checkstyle {
	toolVersion = "8.18"
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-jetty'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		exclude group: 'org.junit.jupiter', module: 'junit-jupiter-engine'
		exclude group: 'junit', module: 'junit'
	}
	testImplementation 'org.springframework.security:spring-security-test'

	compileOnly 'org.projectlombok:lombok:1.18.8'
	annotationProcessor 'org.projectlombok:lombok:1.18.8'

	compile 'io.springfox:springfox-swagger2:2.9.2'
	compile 'io.springfox:springfox-core:2.9.2'
	compile 'io.springfox:springfox-swagger-ui:2.9.2'

	developmentOnly("org.springframework.boot:spring-boot-devtools")

	testImplementation 'org.mockito:mockito-core:2.1.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
	integrationTestImplementation.extendsFrom testImplementation
}

task integrationTest(type: Test) {
	useJUnitPlatform()
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

test {
	useJUnitPlatform()
}